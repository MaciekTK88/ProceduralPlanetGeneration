//---------------------------------------------------------------------------------------
//Write your planet generation logic here. Use the PlanetPos variable as the position for noise sampling.
//Noise functions are available in the NoiseLib.usf file.
//PlanetType is the same variable you set in planet data asset.
//To apply changes use Apply Shader Changes button in the planet actor details panel.
//---------------------------------------------------------------------------------------

#ifndef TERRAINSHADER_USF
#define TERRAINSHADER_USF

#include "GenerationUtilities.usf"
#include "NoiseLib.usf"


TerrainData GetTerrainData(float3 vertexPos, float3 normalizedPlanetPos)
{
    TerrainData terrainData;
    terrainData.finalElevation = 0.0;
    terrainData.erosion = 0;
    terrainData.forestNoise = 0;
    terrainData.top3BiomeIndices = int3(0, 0, 0);
    terrainData.top3BiomeStrengths = float3(1, 0, 0);
        
#if PlanetType == 0

    vertexPos *= 0.00000015;
    
    float erosion = 0.0;
    float finalElevation;
    
    float3 shiftedPos = warpPosition(vertexPos * 0.25, 0.25);
    
    float tectonicEffect = computeInstantPlateEffect(shiftedPos, 0.6) * 10 + 0.1;
    
    float HillNoise   = ErosionHeightTriplanar(vertexPos * 40, 1, erosion) / 1.25;
    HillNoise += fbmE(vertexPos * (200.0), 12, 1.0) * 0.02;


    
    // Use this function to sample the biome terrain curves that you set up in planet data and to fill the top3BiomeIndices and top3BiomeStrengths
    float3 curveHeight = sampleBiomes(vertexPos, tectonicEffect, HillNoise, tectonicEffect, erosion, terrainData.top3BiomeIndices, terrainData.top3BiomeStrengths);
    HillNoise = curveHeight.x;
    tectonicEffect = curveHeight.y;
    erosion = curveHeight.z;
    
    finalElevation = HillNoise * max(tectonicEffect, 0.3);
    finalElevation += tectonicEffect / 1.5;

    finalElevation = flattenElevation(finalElevation, 0, 0.005);

    terrainData.erosion = erosion; // Access in material using vertex color red channel
    terrainData.finalElevation = finalElevation;


    // Index 1 is Forest Flag
    if (ReadBiomeDataTexture(terrainData.top3BiomeIndices.x, 1) >= 0.95) // Check if biome is forest biome
    {
        terrainData.forestNoise = uint(saturate((fbmE(vertexPos * 20, 3, 1.0) - 0.6) * 5) * 255);
    }


        
#else
    vertexPos *= 0.00000015;
    float finalElevation = ((fbmCraters(vertexPos)) * 2 - 1) * 2.0;
    finalElevation = finalElevation * 0.5 + 1.5;
    
    finalElevation += (fbmE(vertexPos * 100.0, 5, 1.0) * 2 - 1) * 0.007;
    terrainData.finalElevation = finalElevation;
        
#endif
    

    return terrainData;
    
}

#endif